plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.hilt)
    alias(libs.plugins.kapt)
}

Properties properties = new Properties()
properties.load(project.rootProject.file("local.properties").newDataInputStream())

android {
    namespace 'com.chaeny.busoda'
    compileSdk libs.versions.compileSdk.get().toInteger()

    defaultConfig {
        applicationId "com.chaeny.busoda"
        minSdk libs.versions.minSdk.get().toInteger()
        targetSdk libs.versions.targetSdk.get().toInteger()
        versionCode 9
        versionName "1.8"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        if (properties['MAPS_API_KEY'] != null) {
            manifestPlaceholders += [MAPS_API_KEY: properties['MAPS_API_KEY']]
        }
    }

    signingConfigs {
        release {
            if (properties['STORE_FILE'] != null) {
                storeFile file(properties['STORE_FILE'])
                storePassword properties['STORE_PASSWORD']
                keyAlias properties['KEY_ALIAS']
                keyPassword properties['KEY_PASSWORD']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        compose true
    }
}

dependencies {
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.activity.compose
    implementation libs.androidx.material3

    implementation libs.hilt.android
    kapt libs.hilt.compiler

    implementation libs.navigation.compose

    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core

    implementation project(":feature:nearbystops")
    implementation project(":feature:favorites")
    implementation project(":feature:stoplist")
    implementation project(":feature:stopdetail")
    implementation project(":core:ui-impl")
    implementation project(":core:ui")
}
